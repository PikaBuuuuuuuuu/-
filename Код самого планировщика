Код самого планировщика:

//НАЧАЛО РАЗРАБОТКИ : 2.07.2024 
Перем ДатаЭлемента Экспорт;
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ,СтандартнаяОбработка) 
	//Пометка текущего дня в календаре
	Дата = ТекущаяДата();
	
	//Отображение рабочих часов
	ОбновитьПериодОтображения();
	
	//Заполнние планировщика
	ЗаполнитьЖурнал();  
	
	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = ЛОЖЬ;
	
	//Добавление столбиков (Автоматическое)
	Измерение = Планировщик.Измерения.Добавить("Помещение");
	Выборка = Справочники.КонференцЗал.Выбрать();
	Пока Выборка.Следующий()Цикл
		Если Выборка.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
	    ЗначениеИзмерения = Измерение.Элементы.Добавить(Выборка.Ссылка); 
		ЗначениеИзмерения.Текст = Выборка.Наименование;
		ЗначениеИзмерения.ЦветРамки = WebЦвета.Золотистый;
	КонецЦикла;
	
КонецПроцедуры 



  //Добавление новой брони в рантайм
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Изменить_Запись" Тогда
		ЗаполнитьЖурнал();
	КонецЕсли;  
	
КонецПроцедуры


#КонецОбласти  

#Область ОбработчикиСобытийЭлементовФормы
//Заполнение докумета БЗДК из планировщка
&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка) 
	
	СтандартнаяОбработка = Ложь;
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Дата", Начало);
	ЗначенияЗаполнения.Вставить("ДатаОкончания", Конец);
	ЗначенияЗаполнения.Вставить("Зал", ЗначенияИзмерений["Помещение"]);
	
	СтруктураПараметров = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.БронированиеЗалаДляКонференций.ФормаОбъекта", СтруктураПараметров);
	
КонецПроцедуры


//При выделении имеющейся брони, отображает форму документа этой брони
&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка) 
	
	СтандартнаяОбработка = Ложь;
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	
	ЭлментПланировщика = ВыделенныеЭлементы[0];
	СтруктураПараментров = Новый Структура ("Ключ", ЭлментПланировщика.Значение);
	ОткрытьФорму("Документ.БронированиеЗалаДляКонференций.ФормаОбъекта", СтруктураПараментров);
	

КонецПроцедуры


&НаКлиенте
Процедура датаПриИзменении(Элемент)
	ОбновитьПериодОтображения();
	ЗаполнитьЖурнал();
	//Сообщить(Планировщик.Элементы.Количество());
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции 
//Ограничение области одного дня. (Рабочий день с 8:30 да 17:30)
  &НаСервере
Процедура ОбновитьПериодОтображения()
	
	Начало = НачалоДня(Дата) + 8.5 * 3600;
	Конец = НачалоДня(Дата) + 17.5 * 3600;
	
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(Начало, Конец);
КонецПроцедуры

 //Вытягивание данных из бд
&НаСервере
Процедура ЗаполнитьЖурнал() 
	
	
	Планировщик.Элементы.Очистить();
	
	//Вытягивание данных из бд		
  		
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БронированиеЗалаДляКонференций.Дата КАК Дата,
		|	БронированиеЗалаДляКонференций.Зал КАК Зал,
		|	БронированиеЗалаДляКонференций.Отдел КАК Отдел,
		|	БронированиеЗалаДляКонференций.БронирующиеЛицо КАК БронирующиеЛицо,
		|	БронированиеЗалаДляКонференций.ЦельБронирования КАК ЦельБронирования,
		|	БронированиеЗалаДляКонференций.Коментарий КАК Коментарий,
		|	БронированиеЗалаДляКонференций.Длительность КАК Длительность,
		|	БронированиеЗалаДляКонференций.ДатаОкончания КАК ДатаОкончания,
		|	БронированиеЗалаДляКонференций.Ссылка КАК Ссылка,
		|	БронированиеЗалаДляКонференций.Цвет КАК Цвет
		|ИЗ
		|	Документ.БронированиеЗалаДляКонференций КАК БронированиеЗалаДляКонференций
		|ГДЕ
		|	БронированиеЗалаДляКонференций.ПометкаУдаления = Ложь
		|	ИЛИ БронированиеЗалаДляКонференций.Проведен = Истина";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
	
		ЗначенияИзмерений = Новый Соответствие;
		ЗначенияИзмерений.Вставить("Помещение", Выборка.Зал); 
		
		//Распределение по колонкам и определении временного диапазано
		ЭлементПланировщика = Планировщик.Элементы.Добавить(Выборка.Дата, Выборка.ДатаОкончания);
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(ЗначенияИзмерений); 
		
		//Отмена перетаскивания элементов планировщика
		ЭлементПланировщика.РежимРазрешенияРедактирования = РежимРазрешенияРедактированияЭлементаПланировщика.ЗапретитьПеретаскиваниеИРастягивание;
		
		//Заполнение выделенного времени информацией
		ЭлементПланировщика.Значение = Выборка.Ссылка;
		
		ТекстЭлемента = Символы.ПС + Строка("Бронирующий: " + Выборка.БронирующиеЛицо) + Символы.ПС + Строка("Отдел: " + Выборка.Отдел) + Символы.ПС + Строка("Тема: " + Выборка.ЦельБронирования) + Символы.ПС + Строка("Комментарий: " + Выборка.Коментарий);
		
		ЭлементПланировщика.Текст = ТекстЭлемента;
		
		//Заливка цеветом из документ, если он не выбран то стандартным
		Цвет = Выборка.Цвет.Получить();
		Если Цвет = Неопределено Тогда  
			
			Цвет = WebЦвета.Индиго; 
			
		КонецЕсли;
		
		ЭлементПланировщика.ЦветФона = Цвет;
	
	КонецЦикла;
	
КонецПроцедуры

//Процедура для оперативного изменений строк документа при переносе его в планировщике
&НаСервереБезКонтекста
Процедура ОбновитьДанныеЗаписи(Записать, ЗначенияРеквизитов)
	
	ОбъектЗаписи = Записать.ПолучитьОбъект();
	
	//Проверка изменений на предмет их наличия
	ЕстьИзменения = Ложь;
	Для каждого Реквизит Из  ЗначенияРеквизитов Цикл 
		Если ОбъектЗаписи[Реквизит.Ключ] <>  Реквизит.Значение Тогда
			ЕстьИзменения = Истина;
			Прервать;
		КонецЕсли;		
	КонецЦикла;
	
	//Запись изменений если таковыи были
	Если ЕстьИзменения  Тогда
		ЗаполнитьЗначенияСвойств(ОбъектЗаписи, ЗначенияРеквизитов); 
		ОбъектЗаписи.Длительность = (ОбъектЗаписи.ДатаОкончания - ОбъектЗаписи.Дата) / 60;
		ОбъектЗаписи.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
	
	
КонецПроцедуры


 // отмена вызова окна бысторого редактирования
 &НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОЖЬ;
    	//сообщить("ПланировщикПередНачаломБыстрогоРедактирования");

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ) 
	Отказ = истина;
КонецПроцедуры







#КонецОбласти

 //КОНЕЦ РАЗРАБОТКИ 11.07.2024
